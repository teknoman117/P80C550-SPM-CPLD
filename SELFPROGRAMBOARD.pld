/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at https://mozilla.org/MPL/2.0/. */

Name     SelfProgramBoard ;
PartNo   AtmelCPLD;
Date     7/12/2021 ;
Revision 01 ;
Designer Nathaniel R Lewis ;
Company  HooDooNet ;
Assembly  None ;
Location  Willits ;
Device   f1504plcc44 ;

PROPERTY ATMEL {JTAG=OFF};
PROPERTY ATMEL {preassign keep};
PROPERTY ATMEL {security on};
PROPERTY ATMEL {pin_keep OFF} ; /* Disables pin-keeper circuits */

/* **** Bus Interface **** */

Pin [4..9, 11, 12] = [D7..0];
Pin [13..14] = [CODE_CS, CODE_RD];
Pin [34, 36..41] = [A0, A10..15];
Pin [44, 43] = [RD, WR];

/* **** Peripherals **** */

Pin [16..21] = [MA0..5];
Pin [24..25] = [MEM_RD, MEM_WR];

Pin 33 = SPI_INT;
Pin 29 = SPI_CLK;
Pin 31 = SPI_OUT;
Pin 32 = SPI_IN;
Pin [26..28] = [SPI_SS0..2];

/* **** Global Signals **** */
 
Pin 2 = CLK;
Pin 1 = RST;

/* **** Address Decoding **** */

Field Address = [A15..0];
PinNode = [Configure_Register, SPI_Data_Register, SPI_Control_Register, Memory_Window];

Configure_Register = Address:'h'[8000..83FF];
SPI_Data_Register = Address:'b'100001XXXXXXXXX0;
SPI_Control_Register = Address:'b'100001XXXXXXXXX1;
Memory_Window = Address:'h'[A000..BFFF];

/* **** Configure Register **** */

PinNode = [Offboard_Select, Delay2..0];
Offboard_Select.D = D6.io;
Offboard_Select.CE = Configure_Register;
Offboard_Select.CK = WR;
Offboard_Select.AR = !RST;

/* Give 3 code reads before switching to SPM memory */
[Delay2..0].D = [Delay1..0, Offboard_Select];
[Delay2..0].CK = CODE_RD;
[Delay2..0].AR = !RST;

CODE_CS = Delay2;

PinNode = [WA0..5];
[WA0..5].D = [D0..5].io;
[WA0..5].CE = Configure_Register;
[WA0..5].CK = WR;
[WA0..5].AR = !RST;

/* **** SPM memory logic **** */

!MEM_RD = (Memory_Window & !RD) # (CODE_CS & !CODE_RD);
!MEM_WR = (Memory_Window & !WR);

[MA0..5] = (!CODE_CS # CODE_RD) & [WA0..5];

/* **** SPI counter register **** */
PinNode = SPI_Running;
PinNode = SPI_Last_Edge;
PinNode = [SPI_Data_Load, SPI_Control_Load];
PinNode = [SPI_Prescaler0..1];

SPI_Data_Load = !WR & SPI_Data_Register;
SPI_Control_Load = !WR & SPI_Control_Register;

[SPI_Prescaler0..1].D = [D0..1].io;
[SPI_Prescaler0..1].CE = SPI_Control_Register & !SPI_Running;
[SPI_Prescaler0..1].CK = WR;
[SPI_Prescaler0..1].AR = !RST;

PinNode = [Prescaler0..5, CNT0..3];

[Prescaler0..5].CK = CLK;

[Prescaler0..1].AR = !RST # [SPI_Prescaler1..0]:0;
[Prescaler2..3].AR = !RST # [SPI_Prescaler1..0]:0 # [SPI_Prescaler1..0]:1;
[Prescaler4..5].AR = !RST # [SPI_Prescaler1..0]:0 # [SPI_Prescaler1..0]:1 # [SPI_Prescaler1..0]:2;

Prescaler0.T = SPI_Running;
Prescaler1.T = SPI_Running & Prescaler0;
Prescaler2.T = SPI_Running & [Prescaler0..1]:&;
Prescaler3.T = SPI_Running & [Prescaler0..2]:&;
Prescaler4.T = SPI_Running & [Prescaler0..3]:&;
Prescaler5.T = SPI_Running & [Prescaler0..4]:&;

PinNode = SPI_Count_Enable;
SPI_Count_Enable = ([SPI_Prescaler1..0]:0 & SPI_Running)
                 # ([SPI_Prescaler1..0]:1 & SPI_Running & [Prescaler0..1]:&)
                 # ([SPI_Prescaler1..0]:2 & SPI_Running & [Prescaler0..3]:&)
                 # ([SPI_Prescaler1..0]:3 & SPI_Running & [Prescaler0..5]:&);

[CNT0..3].CK = CLK;
[CNT0..3].AR = !RST;

CNT0.T = SPI_Count_Enable;
CNT1.T = SPI_Count_Enable & CNT0;
CNT2.T = SPI_Count_Enable & [CNT0..1]:&;
CNT3.T = SPI_Count_Enable & [CNT0..2]:&;

SPI_CLK = CNT0;
SPI_Last_Edge = SPI_Count_Enable & [CNT0..3]:&;

/* **** SPI Data Register **** */

PinNode = [MOSI7..0];
[MOSI7..0].D = [MOSI6..0, 'b'0];
[MOSI7..0].CK = CLK;
[MOSI7..0].CE = SPI_Count_Enable & CNT0;
[MOSI7..0].AR = !RST # (SPI_Data_Load & ![D7..0].io);
[MOSI7..0].AP = (SPI_Data_Load & [D7..0].io);
SPI_OUT = MOSI7;

PinNode = [MISO7..0];
[MISO7..0].D = [MISO6..0, SPI_IN];
[MISO7..0].CK = CLK;
[MISO7..0].CE = SPI_Count_Enable & !CNT0;
[MISO7..0].AR = !RST;

/* **** SPI Control Register **** */

PinNode = [SPI_Interrupt_Enable, SPI_Interrupt_Flag];

SPI_Interrupt_Enable.D = D6.io;
SPI_Interrupt_Enable.CE = SPI_Control_Register;
SPI_Interrupt_Enable.CK = WR;
SPI_Interrupt_Enable.AR = !RST;

SPI_Interrupt_Flag.D = 'b'1;
SPI_Interrupt_Flag.CE = SPI_Last_Edge;
SPI_Interrupt_Flag.CK = CLK;
SPI_Interrupt_Flag.AR = !RST # (SPI_Control_Load & D7.io);

SPI_INT = 'b'0;
SPI_INT.oe = SPI_Interrupt_Enable & SPI_Interrupt_Flag;

SPI_Running.D = 'b'1;
SPI_Running.CE = SPI_Data_Register;
SPI_Running.CK = WR;
SPI_Running.AR = !RST # SPI_Interrupt_Flag;

PinNode = [SS0..1];
[SS0..1].D = [D2..3].io;
[SS0..1].CE = SPI_Control_Register & !SPI_Running;
[SS0..1].CK = WR;
[SS0..1].AR = !RST;

!SPI_SS0 = [SS1..0]:1;
!SPI_SS1 = [SS1..0]:2;
!SPI_SS2 = [SS1..0]:3;

/* **** Multiplexed Bus Data Read **** */

D7.oe = !RD & (SPI_Data_Register # SPI_Control_Register);
[D0..6].oe = !RD & (Configure_Register # SPI_Data_Register # SPI_Control_Register);
[D0..7] = (Configure_Register & [WA0..5, Offboard_Select, 'b'0])
        # (SPI_Data_Register & [MISO0..7])
        # (SPI_Control_Register & [SPI_Prescaler0..1, SS0..1, 'b'0, SPI_Running, SPI_Interrupt_Enable, SPI_Interrupt_Flag]);
